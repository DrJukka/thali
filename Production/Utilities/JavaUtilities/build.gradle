/*
Copyright (c) Microsoft Open Technologies, Inc.
All Rights Reserved
Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
License. You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0

THIS CODE IS PROVIDED ON AN *AS IS* BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED,
INCLUDING WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE, FITNESS FOR A PARTICULAR PURPOSE,
MERCHANTABLITY OR NON-INFRINGEMENT.

See the Apache 2 License for the specific language governing permissions and limitations under the License.
*/

apply plugin: 'java'
apply plugin: 'maven'
apply from: 'javafx.plugin'
apply plugin: 'java-library-distribution'

sourceCompatibility = '1.6'

repositories {
    mavenLocal()
    maven { url "http://thaliartifactory.cloudapp.net/artifactory/libs-snapshot" }
    maven { url "http://thaliartifactory.cloudapp.net/artifactory/libs-release" }
    maven { url 'http://files.couchbase.com/maven2/' }
    mavenCentral()
}

javafx {
    mainClass = 'com.msopentech.thali.utilities.java.JavaFXBridgeManager'
}

dependencies {
    compile 'com.couchbase.lite:java:' + System.getProperty('MAVEN_UPLOAD_VERSION')
    compile 'com.msopentech.thali:ThaliUtilitiesUniversal:' + System.getProperty('MAVEN_UPLOAD_VERSION')
    compile 'com.msopentech.thali:ThaliOnionProxyJava:' + System.getProperty('MAVEN_UPLOAD_VERSION')
    compile 'com.msopentech.thali:BriarJtorctl:' + System.getProperty('MAVEN_UPLOAD_VERSION')

    testCompile 'com.jayway.jsonpath:json-path-assert:0.9.1'
    testCompile 'junit:junit:3.8.2'
    testCompile 'org.slf4j:slf4j-simple:1.7.7'
    testCompile 'org.seleniumhq.selenium:selenium-java:2.42.2'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: System.getProperty('MAVEN_UPLOAD_REPO_URL')) {
                authentication(userName: System.getProperty('MAVEN_UPLOAD_USERNAME'), password: System.getProperty('MAVEN_UPLOAD_PASSWORD')) 
            }

            pom.version = System.getProperty('MAVEN_UPLOAD_VERSION')
            pom.groupId = 'com.msopentech.thali'
            pom.artifactId = 'ThaliUtilitiesJava'
            pom.project {
                licenses {
                    license {
                        name 'Apache 2.0'
                        url 'https://thali.codeplex.com/SourceControl/latest#license.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

task generateJavadocs(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    source = sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
    archives generateJavadocs
}

// Tests are copied over from Android
task deleteTestFiles(type: Delete) {
    delete 'src/test/java/com/msopentech/thali/utilities/test'
}

task copyTestFiles(type: Copy, dependsOn: deleteTestFiles) {
    from "../AndroidUtilities/AndroidUtilities/src/androidTest/java/com/msopentech/thali/utilities/test"
    into 'src/test/java/com/msopentech/thali/utilities/test'
}

compileTestJava.dependsOn(copyTestFiles)
