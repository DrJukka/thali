/*
Copyright (c) Microsoft Open Technologies, Inc.
All Rights Reserved
Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
License. You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0

THIS CODE IS PROVIDED ON AN *AS IS* BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED,
INCLUDING WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE, FITNESS FOR A PARTICULAR PURPOSE,
MERCHANTABLITY OR NON-INFRINGEMENT.

See the Apache 2 License for the specific language governing permissions and limitations under the License.
*/

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}
apply plugin: 'android-library'
apply plugin: 'maven'

repositories {
    // support Android. So the following is a temporary work around for the 1.9 bug until we get a release of Gradle
    // that both supports Android and mavenLocal(). Fix taken from: https://plus.google.com/+XavierDucrohet/posts/hF7W59uZ7rX
    // Note that the fix used System.env.HOME but for some reason that didn't work so I switched it to System.getProperty('user.home') which
    // does work.
    //mavenLocal()
    maven { url "${System.getProperty('user.home')}/.m2/repository" }
    maven { url "http://thaliartifactory.cloudapp.net/artifactory/libs-snapshot" }
    maven { url "http://thaliartifactory.cloudapp.net/artifactory/libs-release" }
    maven { url 'http://files.couchbase.com/maven2/' }
    mavenCentral()
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.1.1"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 18
    }

    // Needed to work around https://code.google.com/p/android/issues/detail?id=61573#c14
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    compile 'org.ektorp:org.ektorp.android:1.4.2-SNAPSHOT'
    compile "com.msopentech.thali:ThaliUtilitiesUniversal:0.0.2"
    compile "com.couchbase.lite:Listener:1.0.0-beta"

    instrumentTestCompile files('libs/td_collator_so.jar')  //
    //fileTree(dir: 'libs', include: '*.jar')
    // This is a local build of an unreleased version of slf4j that fixes the double declaration issue they have
    // that makes it impossible to use Java code compiled with their 'base' JAR work with Android code.
    instrumentTestCompile 'org.slf4j:slf4j-android:1.7.2'
}

def mavenPath() {
    artifactory_local == "true" ?
            'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath :
            artifactory_contextUrl + "/" + (artifactory_snapshot == "true" ? "libs-snapshot-local" : "libs-release-local")
}
def mavenVersionSnapshot() { artifactory_snapshot == "true" ? "-SNAPSHOT" : ""}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: mavenPath()) {
                authentication(userName: "${artifactory_user}", password: "${artifactory_password}")
            }

            pom.version = "0.0.1"
            pom.groupId = 'com.msopentech.thali'
            pom.artifactId = 'ThaliUtilitiesAndroid'
            pom.project {
                licenses {
                    license {
                        name 'Apache 2.0'
                        url 'https://thali.codeplex.com/SourceControl/latest#license.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

task installLocal(dependsOn: uploadArchives)


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}
