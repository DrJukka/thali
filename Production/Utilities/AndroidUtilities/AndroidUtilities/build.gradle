/*
Copyright (c) Microsoft Open Technologies, Inc.
All Rights Reserved
Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
License. You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0

THIS CODE IS PROVIDED ON AN *AS IS* BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED,
INCLUDING WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE, FITNESS FOR A PARTICULAR PURPOSE,
MERCHANTABLITY OR NON-INFRINGEMENT.

See the Apache 2 License for the specific language governing permissions and limitations under the License.
*/

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.10.+'
    }
}

apply plugin: 'android-library'
apply plugin: 'maven'

repositories {
    mavenLocal()
    maven { url "http://thaliartifactory.cloudapp.net/artifactory/libs-snapshot" }
    maven { url "http://thaliartifactory.cloudapp.net/artifactory/libs-release" }
    maven { url 'http://files.couchbase.com/maven2/' }
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 19
    }

    // Needed to work around https://code.google.com/p/android/issues/detail?id=61573#c14
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE.txt' //https://groups.google.com/forum/#!topic/adt-dev/bl5Rc4Szpzg
        exclude 'META-INF/LICENSE.txt'
        pickFirst 'BridgeHandlerTest.js' // Causing a double import of ThaliUtilitiesUniversal for no apparent reason
        pickFirst 'BridgeManager.js' // See previous
        exclude 'LICENSE.txt' // This is from the hamcrest-library we use for testing brought in by jayway's jsonpath
    }
}

dependencies {
    compile 'com.couchbase.lite:couchbase-lite-android:' + System.getProperty('MAVEN_UPLOAD_VERSION')
    compile 'com.msopentech.thali:ThaliUtilitiesUniversal:' + System.getProperty('MAVEN_UPLOAD_VERSION')
    compile 'com.msopentech.thali:ThaliOnionProxyAndroid:' + System.getProperty('MAVEN_UPLOAD_VERSION')
    compile 'org.ektorp:org.ektorp.android:1.4.2'

    //fileTree(dir: 'libs', include: '*.jar')
    androidTestCompile 'org.slf4j:slf4j-android:1.7.7'
    androidTestCompile 'com.jayway.jsonpath:json-path-assert:0.9.1'

}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: System.getProperty('MAVEN_UPLOAD_REPO_URL')) {
                authentication(userName: System.getProperty('MAVEN_UPLOAD_USERNAME'), password: System.getProperty('MAVEN_UPLOAD_PASSWORD')) 
            }

            pom.version = System.getProperty('MAVEN_UPLOAD_VERSION')
            pom.groupId = 'com.msopentech.thali'
            pom.artifactId = 'ThaliUtilitiesAndroid'
            pom.project {
                licenses {
                    license {
                        name 'Apache 2.0'
                        url 'https://thali.codeplex.com/SourceControl/latest#license.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}
